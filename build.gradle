import io.papermc.paperweight.userdev.ReobfArtifactConfiguration

//import xyz.jpenilla.resourcefactory.bukkit.BukkitPluginYaml

plugins {
    id 'java-library'
    id 'maven-publish'
    id ("io.papermc.paperweight.userdev") version "1.7.2"
    id("xyz.jpenilla.run-paper") version "2.3.0" // Adds runServer and runMojangMappedServer tasks for testing
//    id("xyz.jpenilla.resource-factory-bukkit-convention") version "1.1.1"
}



repositories {

    mavenCentral()
    maven { url "https://jitpack.io" }
    maven { url = 'https://maven.maxhenkel.de/repository/public' }
    maven {
        name = "papermc-repo"
        url = "https://repo.papermc.io/repository/maven-public/"
    }
    maven {
        name = "sonatype"
        url = "https://oss.sonatype.org/content/groups/public/"
    }
}

dependencies {
    paperweight.paperDevBundle("${minecraft_version}-${paper_build}")
    implementation "de.maxhenkel.voicechat:voicechat-api:${voicechat_api_version}"

}
paperweight.reobfArtifactConfiguration = ReobfArtifactConfiguration.getMOJANG_PRODUCTION()
def targetJavaVersion = Integer.parseInt(java_version)
java {
    withSourcesJar()
    withJavadocJar()
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'

    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release.set(targetJavaVersion)
    }
}

processResources {
    def props = [version: plugin_version, group: rootProject.group]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('paper-plugin.yml') {
        expand props
    }
}
publishing {

    publications {
        maven(MavenPublication) {
            from components.java

        }
    }
    repositories {
        maven {
            name = 'localRepo'
            url = layout.buildDirectory.dir("repo")
        }
    }
}